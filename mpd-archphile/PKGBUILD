# Maintainer: Tux <info@archphile.org>
# Based on mpd-light from AUR made by the following people:
# Maintainer: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Ben <ben@benmazer.net>

pkgname=mpd-archphile
pkgver=0.19.1	
pkgrel=3
pkgdesc='Flexible, powerful, server-side application for playing music. Light version optimized for Archphile distribution'
url='http://www.musicpd.org/'
license=('GPL')
arch=('armv6h' 'armv7h')
depends=('audiofile' 'libmad' 'curl' 'faad2' 'sqlite' 'libmms' 'libid3tag' 'libmpdclient' 'boost-libs' 'libsoxr')
makedepends=('boost' 'doxygen')
provides=("mpd=$pkgver")
conflicts=('mpd' 'mpd-archphile-ffmpeg')
replaces=('mpd')
source=("http://www.musicpd.org/download/mpd/0.19/mpd-${pkgver}.tar.xz"{,.sig}
        'tmpfiles.d'
	'mpd.conf')
sha1sums=('68f1ff43a2dd4de913d6c979db504dc2955f5737'
          'SKIP'
          'f4d5922abb69abb739542d8e93f4dfd748acdad7'
          'bb6e5a1e9202ae2a0ea26246d9ff78f7447b0fb8')
backup=('etc/mpd.conf')
install=mpd.install

prepare() {
	cd "${srcdir}/mpd-${pkgver}"
	touch systemd/mpd.socket
}

build() {
	cd "${srcdir}/mpd-${pkgver}"

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-libmpdclient \
		--enable-mad \
		--enable-soxr \
		--disable-ao \
		--disable-ffmpeg \
		--disable-jack \
		--disable-modplug \
		--disable-pulse \
		--disable-shout \
		--disable-sidplay \
		--disable-soundcloud \
		--disable-wavpack \
		--disable-opus \
		--disable-lame-encoder \
		--disable-vorbis \
		--disable-ipv6 \
		--disable-recorder-output \
		--disable-iso9660 \
		--disable-zzip \
		--disable-wildmidi \
		--disable-oss \
		--disable-fluidsynth \
		--disable-gme \
		--disable-bzip2 \
		--with-zeroconf=no \
		--with-systemdsystemunitdir=/usr/lib/systemd/system

	make
}

package() {
	cd "${srcdir}/mpd-${pkgver}"
	
	make DESTDIR="${pkgdir}" install

	install -Dm644 ../mpd.conf "${pkgdir}"/etc/mpd.conf
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/mpd.conf
	install -d -g 45 -o 45 "${pkgdir}"/var/lib/mpd/playlists

	install -Dm644 "${pkgdir}"/usr/lib/systemd/{system,user}/mpd.service
	sed '/\[Service\]/a User=mpd' -i "${pkgdir}"/usr/lib/systemd/system/mpd.service
	sed '/WantedBy=/c WantedBy=default.target' -i "${pkgdir}"/usr/lib/systemd/user/mpd.service
        sed '/ExecStart=/c ExecStart=/usr/bin/nice -n -16 /usr/bin/mpd --no-daemon' -i "${pkgdir}"/usr/lib/systemd/{system,user}/mpd.service

}
