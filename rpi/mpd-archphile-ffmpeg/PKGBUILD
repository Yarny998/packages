# Maintainer: Tux <info@archphile.org>
# Based on mpd-light from AUR made by the following people:
# Maintainer: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Ben <ben@benmazer.net>

pkgname=mpd-archphile-ffmpeg
pkgver=0.18.16
pkgrel=2
pkgdesc='Flexible, powerful, server-side application for playing music. Light version with ffmpeg support optimized for Archphile 
distribution'
url='http://www.musicpd.org/'
license=('GPL')
arch=('armv6h')
depends=('audiofile' 'libmad' 'curl' 'faad2' 'sqlite' 'libmms' 'libid3tag' 'libmpdclient' 'ffmpeg')
makedepends=('doxygen')
provides=("mpd=$pkgver")
conflicts=('mpd' 'mpd-archphile')
replaces=('mpd')
source=("http://www.musicpd.org/download/mpd/${pkgver%.*}/mpd-${pkgver}.tar.xz"
        'tmpfiles.d'
	'mpd.conf')
sha1sums=('ef510446e858fadf20d36fa2c1bed6f35a51e613'
          'f4d5922abb69abb739542d8e93f4dfd748acdad7'
          'f1ec91c92c0a5dd2648ab45f27e440a73b0e15ce')
backup=('etc/mpd.conf')
install=mpd.install

build() {
	cd "${srcdir}/mpd-${pkgver}"

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-libmpdclient \
		--enable-mad \
		--disable-ao \
		--enable-ffmpeg \
		--disable-jack \
		--disable-modplug \
		--disable-pulse \
		--disable-shout \
		--disable-sidplay \
		--disable-soundcloud \
		--disable-wavpack \
		--disable-opus \
		--disable-lame-encoder \
		--disable-vorbis \
		--disable-ipv6 \
		--disable-recorder-output \
		--disable-iso9660 \
		--disable-zzip \
		--disable-wildmidi \
		--disable-oss \
		--disable-fluidsynth \
		--disable-gme \
		--disable-bzip2 \
		--with-zeroconf=no \
		--with-systemdsystemunitdir=/usr/lib/systemd/system

	make
}

package() {
	cd "${srcdir}/mpd-${pkgver}"
	
	make DESTDIR="${pkgdir}" install

	install -Dm644 ../mpd.conf "${pkgdir}"/etc/mpd.conf
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/mpd.conf
	install -d -g 45 -o 45 "${pkgdir}"/var/lib/mpd/playlists

	install -Dm644 "${pkgdir}"/usr/lib/systemd/{system,user}/mpd.service
	sed '/WantedBy=/c WantedBy=default.target' -i "${pkgdir}"/usr/lib/systemd/user/mpd.service
        sed '/ExecStart=/c ExecStart=/usr/bin/nice -n -19 /usr/bin/mpd --no-daemon' -i "${pkgdir}"/usr/lib/systemd/{system,user}/mpd.service

}
